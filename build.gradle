plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id "nu.studer.jooq" version "8.2.1" apply false
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

subprojects {
    // 全てのサブプロジェクトに共通の設定
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'nu.studer.jooq'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        runtimeOnly 'org.postgresql:postgresql:42.7.1'
        implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs:3.1.0'

        // jooq
        implementation 'org.springframework.boot:spring-boot-starter-jooq'
        jooqGenerator 'org.postgresql:postgresql:42.7.1'
        jooqGenerator 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
    }

    // jOOQ共通設定
    jooq {
        version = '3.19.1'
        configurations {
            // プロジェクト名を取得
            def projectName = project.name.replaceAll("-", "")
            // 環境変数を取得
            def databaseUrl = System.getenv('DATABASE_URL')
            def postgresUser = System.getenv('POSTGRES_USER')
            def postgresPassword = System.getenv('POSTGRES_PASSWORD')

            main {
                generationTool {
                    jdbc {
                        url = databaseUrl
                        user = postgresUser
                        password = postgresPassword
                    }
                    generator {
                        name = "org.jooq.codegen.JavaGenerator"
                        database {
                            inputSchema = 'public'
                            includes = '.*'
                        }
                        target {
                            packageName = "com.example.${projectName}.infrastructure.jooq"
                            directory = "${project.buildDir}/generated/source/jooq/main"
                        }
                    }
                }
            }
        }
    }

    // main-service と file-service に対して shared モジュールを追加
    if (name == 'main-service' || name == 'file-service') {
        dependencies {
            implementation project(':shared')
        }
    }

    repositories {
        mavenCentral()
    }

    // main-service 特有の設定
    project(':main-service') {
        bootRun {
            mainClass = 'com.example.mainservice.MainServiceApplication'
            jvmArgs = [
                    '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005',
            ]
        }

        dependencies {
        }
    }

    // file-service 特有の設定
    project(':file-service') {
        bootRun {
            mainClass = 'com.example.fileservice.FileServiceApplication'
            jvmArgs = [
                    '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005',
            ]
        }

        dependencies {
        }
    }
}


tasks.named('test') {
    useJUnitPlatform()
}
